\documentclass[10pt,a4paper]{book}

\NewDocumentCommand\chpl{}{\texttt{chpl}}

\book{うさぎさんでもわかる並列言語Chapel}{Chapel the Parallel Programming Language}{1}{Chapel,並列処理,高性能計算,PGAS,HPC}

\begin{document}
\maketitle
\tableofcontents

\chapter{はじめに}

\href{https://chapel-lang.org}{Chapel}は、\textbf{型推論}と\textbf{テンプレート}と\textbf{区分化大域アドレス空間}を特徴とし、高度に抽象化された\textbf{高生産性並列言語}である。
NUMAや分散メモリ環境を得意とし、通常の共有メモリ環境と同様の記述性ながら、高度な並列分散処理を実装できる。

\begin{Verbatim}{bash}
$ VERSION=1.28.0
$ wget https://github.com/chapel-lang/chapel/releases/download/$VERSION/chapel-$VERSION.tar.gz
$ tar xzf chapel-$VERSION.tar.gz && cd chapel-$VERSION && ./configure && sudo make install
\end{Verbatim}

以下の\texttt{hello.chpl}を作成する。\texttt{main}関数は省略できる。コメントの記法は、C言語(C99)と同様だが、ネストできる。

\begin{Verbatim}{Chapel}
/*
multiline comments
 or block comments
*/
writeln("Hello, world!"); // 1-line comments
\end{Verbatim}

\chpl{}でビルドして、\texttt{hello}を生成する。\texttt{--fast}を指定すると、最適化が有効になり、実行時の検査機能が無効化される。
他にも、\texttt{--savec}を指定すると、C言語に変換できる。\texttt{hello}には、\texttt{--help}を始め、便利な機能が自動的に付加される。

\begin{Verbatim}{bash}
$ chpl --fast -o hello --savec savec hello.chpl
$ ./hello
Hello, world!
$ ./hello --help
\end{Verbatim}

Chapelでは、\texttt{module}宣言で\textbf{名前空間}を定義する。関数の外に記述された処理は、名前空間の初期化と同時に実行される。
最も外側の処理は、拡張子\texttt{.chpl}を除外した名前の、名前空間を構成する。これが、\texttt{main}関数を省略できた理由である。

\begin{Verbatim}{Chapel}
module Foo {
	writeln("initilize Foo");
	proc main() {
		writeln("This is Foo");
	}
}
module Bar {
	writeln("initilize Bar");
	proc main() {
		writeln("This is Bar");
	}
}
import baz.Foo;
import baz.Bar;
proc main() {
	Foo.main();
	Bar.main();
}
\end{Verbatim}

複数の名前空間を定義した場合は、どの名前空間に実装された\texttt{main}関数を起動するか、ビルド時に指定する必要がある。

\begin{Verbatim}{bash}
$ chpl --main-module baz baz.chpl
$ ./baz
initilize Foo
initilize Bar
This is Foo
This is Bar
\end{Verbatim}

\chapter{式\label{chap:op}}

C言語と同様に、演算子には、左右の\textbf{結合法則}と\textbf{優先順位}がある。優先順に掲載する。演算の順序は、括弧で調整できる。

\begin{table}[h]
\raggedright
\begin{tabular}{lll}
\textbf{関数適用}     & \verb#. () []#                          & 左結合 \\
\textbf{初期化}       & \verb#new#                              & 右結合 \\
\textbf{所有権}       & \verb#owned shared borrowed unmanaged#  & 右結合 \\
\textbf{ヌル許容}     & \verb#? !#                              & 左結合 \\
\textbf{型変換}       & \verb#:#                                & 左結合 \\
\textbf{累乗}         & \verb#**#                               & 右結合 \\
\textbf{集約}         & \verb#reduce scan dmapped#              & 左結合 \\
\textbf{否定}         & \verb#! ~#                              & 右結合 \\
\textbf{乗除}         & \verb#* / %#                            & 左結合 \\
\textbf{符号}         & \verb#+ -#                              & 右結合 \\
\textbf{シフト}       & \verb#<< >>#                            & 左結合 \\
\textbf{論理積}       & \verb#&#                                & 左結合 \\
\textbf{排他的論理和} & \verb#^#                                & 左結合 \\
\textbf{論理和}       & \verb#|#                                & 左結合 \\
\textbf{加減}         & \verb#+ -#                              & 左結合 \\
\textbf{レンジ}       & \verb#.. ..<#                           & 左結合 \\
\textbf{順序比較}     & \verb#<= => < >#                        & 左結合 \\
\textbf{等値比較}     & \verb#== !=#                            & 左結合 \\
\textbf{短絡論理積}   & \verb#&&#                               & 左結合 \\
\textbf{短絡論理和}   & \verb#||#                               & 左結合 \\
\textbf{刻み幅}       & \verb|by # align|                       & 左結合 \\
\textbf{ループ変数}   & \verb#in#                               & 左結合 \\
\textbf{制御構造式}   & \verb#if for forall sync single atomic# & 左結合 \\
\textbf{区切り}       & \verb#,#                                & 左結合 \\
\end{tabular}
\end{table}

\section{型変換}

Chapelは、\textbf{静的型付け言語}である。また、型変換を明示的に行う場合は、\texttt{:}演算子を使う。以下に、型変換の例を示す。

\begin{Verbatim}{Chapel}
writeln(1 + 2: real); // 3.0
writeln(int: string); // int(64)
\end{Verbatim}

\section{集約演算}

\texttt{reduce}演算子は、右辺の値を集計する。必要に応じて、並列処理が行われる。\texttt{scan}演算子は、集計の過程を順番に返す。
集計の内容は、\texttt{+}や\texttt{*}や\texttt{min}や\texttt{max}を指定できる。\texttt{minloc}や\texttt{maxloc}の場合は、最小値または最大値の位置も取得できる。

\begin{Verbatim}{Chapel}
writeln(+ scan(1..10)); // 1 3 6 10 15 21 28 36 45 55
writeln("value, index: ", minloc reduce zip([3, 2, 1], 0..2)); // value, index: (1, 2)
writeln("value, index: ", maxloc reduce zip([4, 5, 6], 0..2)); // value, index: (6, 2)
\end{Verbatim}

\chapter{変数}

\texttt{var}で宣言された識別子は、\textbf{変数}となる。宣言と同時に型と値を指定できる。自明な場合は、型または値を省略できる。
変数の値は、\textbf{代入演算子}で変更できる。その場合の変数を\textbf{左辺値}と呼ぶ。また、\textbf{スワップ演算子}で両辺の値を交換できる。

\begin{Verbatim}{Chapel}
var foo: int = 12;
var bar: int;
foo = 889464;
var a = "golden axe.";
var b = "silver axe.";
a <=> b;
writeln(a, b, foo);
\end{Verbatim}

\section{定数}

\texttt{const}で宣言された識別子は、\textbf{定数}となる。定数の値は変更できず、初期値で固定される。初期値は実行時に計算される。

\begin{Verbatim}{Chapel}
const foo: int = 12;
const bar = 12;
\end{Verbatim}

\texttt{param}で宣言された識別子は、\textbf{静的定数}となる。定数と同様に初期値で固定され、初期値はコンパイル時に計算される。

\begin{Verbatim}{Chapel}
param foo: int = 12;
param bar = 12;
type num = int;
\end{Verbatim}

\texttt{type}で宣言された識別子は、\textbf{型定数}となる。なお、静的定数には、以下に示す\textbf{基本型}または\textbf{列挙型}の値のみ設定できる。

\begin{Verbatim}{Chapel}
param a: bool = true;
param b: uint = 1919;
param c: real = 8.10;
param d: imag = 364i;
param e: string = 'ABC' + "DEF";
param f: Gibier = Gibier.Rabbit;
enum Gibier {Deer, Boar, Rabbit};
\end{Verbatim}

\section{設定}

\texttt{config}を前置した変数や定数の値は、起動時に変更できる。同様に、静的定数や型定数も、コンパイル時に変更できる。

\begin{Verbatim}{Chapel}
config const bar = 121;
config param foo = 121;
config type num = uint;
writeln(foo, bar, num: string);
\end{Verbatim}

例えば、定数\texttt{bar}は、起動時に\texttt{--bar}で設定できる。静的定数\texttt{foo}と型定数\texttt{num}は、コンパイル時に\texttt{--set}で設定できる。

\begin{Verbatim}{bash}
$ chpl config.chpl --set foo=12321 --set num=real
$ ./config --bar=12321
1232112321real(64)
\end{Verbatim}

\chapter{構文}

Chapelの構文には、Fortranの影響が見られる。\textbf{文}の区切りには、\texttt{;}を記す。基本的に、先頭の文から順番に実行される。
複数の文を括弧で閉じた\textbf{複文}は、静的な\textbf{スコープ}を形成し、内側で宣言された変数や関数は、外側に対して秘匿される。

\begin{Verbatim}{Chapel}
{
	var foo = 12;
	writeln(foo);
}
var foo = 13;
writeln(foo);
\end{Verbatim}

\section{条件分岐}

\texttt{if}文は、\textbf{条件分岐}を行う。条件式は\texttt{bool}型である。\texttt{then}節が復文の場合は、\texttt{then}を省略できる。\texttt{else}節も省略できる。

\begin{Verbatim}{Chapel}
const age = 18;
if age < 18 then {
	writeln("Adults Only");
} else {
	writeln("Yeah, Right");
}
\end{Verbatim}

\texttt{select}文は、\textbf{多分岐}を行う。条件式が合致した\texttt{when}節か\texttt{otherwise}節が実行される。両者とも固有のスコープを持つ。

\begin{Verbatim}{Chapel}
select "cat" {
	when "cat" do writeln("meow");
	when "dog" do writeln("bowwow");
	otherwise writeln("gobblegobble");
}
\end{Verbatim}

\section{反復処理}

\texttt{while do}文は、\texttt{do}節を繰り返す。まず、条件式を評価して、\texttt{true}の場合は\texttt{do}節を実行し、再び条件式の評価に戻る。
\texttt{do while}文も、\texttt{do}節を繰り返す。まず、\texttt{do}節を実行して、条件式を評価する。\texttt{true}の場合は、再び\texttt{do}節を実行する。

\begin{Verbatim}{Chapel}
var i = 1;
while i < 1024 do i *= 2;
do i >>= 1; while i >= 1;
writeln(i); // 0
\end{Verbatim}

\texttt{for}文は、\textbf{イテレータ}が値を返す限り\texttt{do}節を繰り返す。\texttt{while do}文や\texttt{for}文の\texttt{do}節が復文の場合は、\texttt{do}を省略できる。

\begin{Verbatim}{Chapel}
for i in 1..100 do writeln(i);
\end{Verbatim}

反復処理を離脱する場合は、C言語と同様に、\texttt{break}文や\texttt{continue}文を使う。ラベルを指定すれば、\textbf{大域脱出}もできる。

\begin{Verbatim}{Chapel}
while true do break;
for 1..100 do continue;
label outside for i in 1..10 do for j in 1..10 do break outside;
\end{Verbatim}

\chapter{関数\label{chap:proc}}

Chapelの関数には、\textbf{手続き}と\textbf{イテレータ}と演算子の3種類が存在し、予約語の\texttt{proc}と\texttt{iter}と\texttt{operator}で定義できる。
記事では、単に手続きを指して関数と呼ぶ。関数は、\textit{first-class}で、\textbf{テンプレート}の機能を持ち、例外処理も可能である。

\section{定義}

以下の関数\texttt{foo}は、\texttt{int}型の引数\texttt{x}と\texttt{y}を取り、\texttt{return}文で\texttt{int}型の値を返す。関数の呼び方は、C言語と同様である。

\begin{Verbatim}{Chapel}
proc foo(x: int, y: int): int {
	return x + y;
}
writeln(foo(1, 2));
\end{Verbatim}

引数や返り値の型は、省略できる。この場合の関数は、実質的に\textbf{テンプレート関数}であり、型は、実引数から推論される。

\begin{Verbatim}{Chapel}
proc foo(x, y) return x + y;
writeln(foo(1, 2i)); // 1.0 + 2.0i
writeln(foo(2i, 3)); // 3.0 + 2.0i
\end{Verbatim}

引数が0個の場合は、引数の括弧を省略できる。また、内容が\texttt{return}文だけの関数は、処理を囲む括弧\verb|{}|を省略できる。

\begin{Verbatim}{Chapel}
proc foo return 100110;
writeln(foo);
\end{Verbatim}

関数に引数を渡す際に、引数の名前を指定できる。また、引数を省略した場合に渡されるデフォルトの値を設定できる。

\begin{Verbatim}{Chapel}
proc foo(x: int = 0, y: int = 0): int return x + y;
writeln(foo(x = 1, y = 2)); // 3
writeln(foo(y = 2, x = 1)); // 3
writeln(foo(1)); // 1
\end{Verbatim}

関数は、可変長の引数を宣言できる。その実体は\textbf{タプル}である。また、タプルを渡す場合は、展開の演算子\texttt{...}を使う。

\begin{Verbatim}{Chapel}
proc sum(x: int ...): int return + reduce(x);
writeln(sum(1, 2, 3) + sum((...(100, 200)))); // 306
\end{Verbatim}

関数は、他の関数の内側にも定義できる。関数の外側から見ると、内側の関数は秘匿される。また、\textbf{高階関数}にもできる。

\begin{Verbatim}{Chapel}
proc factorial(num: int): int {
	proc tc(n, accum: int): int {
		if n == 0 then return accum;
		return tc(n - 1, n * accum);
	}
	return tc(num, 1);
}
writeln(factorial(10)); // 3628800
\end{Verbatim}

例えば、関数を関数の引数や変数に代入できる。また、関数の名前が必要なければ、\texttt{lambda}で無名の関数を定義できる。

\begin{Verbatim}{Chapel}
proc call(f, x: int, y: int): int return f(x, y);
const add = lambda(x: int, y: int) return x + y;;
writeln(call(add: func(int, int, int), 36, 514)); // 550
\end{Verbatim}

\section{修飾子}

\texttt{inline}で宣言された関数は、\textbf{インライン展開}される。\texttt{export}で宣言された関数は、\textbf{ライブラリ関数}として公開される。

\begin{Verbatim}{Chapel}
inline proc foo(x: int): int return 2 * x;
export proc bar(x: int): int return 2 * x;
writeln(foo(100)); // 200
writeln(bar(300)); // 600
\end{Verbatim}

共有ライブラリで実装された関数を使う場合は、\texttt{extern}で関数を宣言する。以下に、CPUの番号を取得する例を示す。

\begin{Verbatim}{Chapel}
require "sched.h";
extern proc sched_getcpu(): int;
writeln("CPU:", sched_getcpu());
\end{Verbatim}

引数や返り値にも、修飾子を指定できる。\texttt{param}や\texttt{type}で宣言された引数は、定数や型となる。返り値も同様にできる。

\begin{Verbatim}{Chapel}
proc tuplet(param dim: int, type eltType) type return dim * eltType;
const fac: tuplet(9, int) = (1, 1, 2, 6, 24, 120, 720, 5040, 40320);
\end{Verbatim}

\texttt{inout}や\texttt{out}で宣言された引数の値は、関数から戻る際に書き戻される。ただし、\texttt{out}の場合は、実引数が無視される。

\begin{Verbatim}{Chapel}
proc intents(inout x: int, in y: int, out z: int, ref v: int): void {
	x += y;
	z += y;
	v += y;
}
var a: int = 1;
var b: int = 2;
var c: int = 3;
var d: int = 4;
intents(a, b, c, d);
writeln(a, b, c, d); // 3226
\end{Verbatim}

\texttt{ref}で宣言された引数は、\textbf{参照渡し}になる。また、返り値が\texttt{ref}の関数は、左辺値を返すので、代入の構文を定義できる。

\begin{Verbatim}{Chapel}
var tuple = (0, 0, 0, 0, 0, 0, 0, 0, 0);
proc A(i: int) ref: int return tuple(i);
coforall i in tuple.indices do A(i) = i;
writeln(tuple);
\end{Verbatim}

\section{条件分岐}

Chapelの関数は、実質的に\textbf{テンプレート関数}である。関数が\textbf{インスタンス}になる条件は、\texttt{where}節で詳細に指定できる。
\texttt{where}節は、コンパイル時に評価される。例えば、以下の\texttt{whichType}関数は、引数\texttt{x}の型\texttt{xt}に応じて、内容が変化する。

\begin{Verbatim}{Chapel}
proc whichType(x: ?xt): string where xt == real return "x is real";
proc whichType(x: ?xt): string where xt == imag return "x is imag";
proc whichType(x: ?xt): string return "x is neither real nor imag";
writeln(whichType(114.514));
writeln(whichType(364364i));
writeln(whichType(1919810));
\end{Verbatim}

\texttt{where}節の条件分岐は、任意の定数式を扱えるので、\textbf{コンパイル時計算}に利用できる。以下は、階乗を計算する例である。

\begin{Verbatim}{Chapel}
proc fact(param n: int) param: int where n >= 1 return n * fact(n-1);
proc fact(param n: int) param: int where n == 0 return 1;
if fact(8) != 5040 then compilerError("fact(7) == 5040");
\end{Verbatim}

\section{型引数}

任意の値を受け取る引数に、\texttt{?}を前置した\textbf{型引数}を宣言すると、型を取得できる。以下に、配列の型を取得する例を示す。
ただし、型引数を宣言せずに、\texttt{.type}や\texttt{.domain}や\texttt{.eltType}などの\textbf{クエリ式}を活用し、型の詳細を取得する方法もある。

\begin{Verbatim}{Chapel}
proc foo(x: [?dom] ?el) return (dom, el: string);
proc bar(x) return (x.domain, x.eltType: string);
writeln(foo([1, 2, 3, 4, 5, 6, 7, 8])); // ({0..7}, int(64))
writeln(bar([1, 2, 3, 4, 5, 6, 7, 8])); // ({0..7}, int(64))
writeln(foo(["one" => 1, "two" => 2])); // ({one, two}, int(64))
writeln(bar(["one" => 1, "two" => 2])); // ({one, two}, int(64))
\end{Verbatim}

\section{例外処理}

\texttt{throw}文は、異常の発生を通知する。この異常を\textbf{例外}と呼ぶ。\texttt{catch}文で捕捉されるまで、関数が順繰りに巻き戻される。
例外が発生し得る関数は、\texttt{throws}宣言が必要である。また、\texttt{defer}文で予約した処理は、例外が発生しても実行される。

\begin{Verbatim}{Chapel}
proc foo(message: string) throws {
	defer writeln("See you");
	throw new Error(message);
}
\end{Verbatim}

\texttt{catch}文は、\texttt{try}文の内側で例外が発生した場合には、その例外を捕捉し、回復処理を行う役割がある。以下に例を示す。

\begin{Verbatim}{Chapel}
try {
	foo("Hello,");
	foo("world!");
} catch e {
	writeln(e);
}
\end{Verbatim}

\section{演算子}

\texttt{operator}で宣言された関数は、演算子の機能を再定義する。ただし、\chapref{op}に掲載した演算子に限る。以下に例を示す。

\begin{Verbatim}{Chapel}
operator *(text: string, num: int) return + reduce (for 1..num do text);
writeln("Lorem ipsum dolor sit amet, consectetur adipiscing elit" * 10);
\end{Verbatim}

\section{イテレータ}

\texttt{iter}で宣言された関数は、\textbf{イテレータ}となる。\texttt{yield}文は、処理を断ち、指定された値を返し、残りの処理を再開する。
\texttt{these}を実装した構造体も、イテレータとして機能する。\texttt{for}文に渡すと、暗黙的に\texttt{these}が呼ばれる。以下に例を示す。

\begin{Verbatim}{Chapel}
iter iterator(): string {
	yield "EMURATED";
	yield "EMURATED";
	yield "EMURATED";
}
iter int.these() const ref: int {
	for i in 1..this do yield i;
}
var repetition: int = 10;
for i in iterator() do writeln(i);
for i in repetition do writeln(i);
\end{Verbatim}

\chapter{構造体\label{chap:class}}

Chapelでは、構造体を定義して、構造体に変数や関数を定義できる。変数を\textbf{フィールド}と呼び、関数を\textbf{メソッド}と呼ぶ。
構造体は、\textbf{クラス}と\textbf{レコード}と\textbf{共用体}に分類できる。クラスは\textbf{参照型}の性質を、レコードと共用体は\textbf{値型}の性質を持つ。

\section{定義}

以下に、クラスを定義して、\texttt{new}演算子で\textbf{インスタンス}を生成する例を示す。参照型なので、変数には参照が格納される。

\begin{Verbatim}{Chapel}
class Num {
	var r: real;
	var i: imag;
}
var num: Num = new Num();
num.r = 816.07;
num.i = 14.22i;
writeln(num.r);
\end{Verbatim}

以下に、レコードを定義する例を示す。クラスと似た機能だが、値型なので、インスタンスの複製が変数に格納される。

\begin{Verbatim}{Chapel}
record Num {
	var r: real;
	var i: imag;
}
var num: Num;
num.r = 816.07;
num.i = 14.22i;
writeln(num.r);
\end{Verbatim}

共用体は、同じメモリ領域を複数のフィールドで共有する。最後に値を格納したフィールドのみ、意味のある値を持つ。

\begin{Verbatim}{Chapel}
union Num {
	var r: real;
	var i: imag;
}
var num: Num;
num.r = 816.07;
num.i = 14.22i;
writeln(num.i);
\end{Verbatim}

\section{総称型}

総称型は、型引数や静的定数を引数に取る多相型である。型引数が異なると、異なる型として扱われる。以下に例を示す。

\begin{Verbatim}{Chapel}
record Stack {
	type eltType;
	param size: int;
}
var a: Stack(eltType = uint, size = 12);
var b: Stack(eltType = real, size = 24);
writeln("a is ", a.type: string); // a is Stack(uint(64), 12)
writeln("b is ", b.type: string); // b is Stack(real(64), 24)
\end{Verbatim}

\section{メソッド}

構造体には、メソッドを定義できる。また、メソッドでインスタンスを参照する場合は、\texttt{this}を使う。以下に例を示す。

\begin{Verbatim}{Chapel}
record User {
	var name: string;
	proc set(name) {
		this.name = name;
	}
}
var user: User;
user.set("Alicia");
writeln(user.name);
\end{Verbatim}

なお、値型の性質を持つレコードでも、\texttt{this}はインスタンスへの参照であり、意図通りにフィールドの値を変更できる。
既存の構造体にも、メソッドを追加できる。また、フィールドと同名で、参照と代入を隠蔽する、\textbf{アクセサ}を定義できる。

\begin{Verbatim}{Chapel}
record User {
	var name: string;
}
proc User.name ref {
	writeln(".name");
	return this.name;
}
var user: User;
user.name = "Jane";
writeln(user.name);
\end{Verbatim}

\texttt{this}メソッドが定義された構造体は、関数と同様に振る舞う。引数を与えると、暗黙的に\texttt{this}メソッドが実行される。

\begin{Verbatim}{Chapel}
class Add {
	proc this(a: int, b: int): int return a + b;
}
const add = new Add();
writeln(add(123, 45)); // 168
\end{Verbatim}

\texttt{init}メソッドが定義された構造体は、引数を与えてインスタンスを生成すると、暗黙的に\texttt{init}メソッドが実行される。
\texttt{init}メソッドは、フィールドを初期化できる。定数の場合は、\texttt{init}メソッドで初期化する必要がある。以下に例を示す。

\begin{Verbatim}{Chapel}
class Add {
	const x: real;
	const y: real;
	proc init(x, y) {
		this.x = x;
		this.y = y;
	}
}
const add = new Add(x = 1, y = 2);
writeln("x, y: ", (add.x, add.y));
\end{Verbatim}

\texttt{init}メソッドは、省略しても自動的に定義される。また、\texttt{deinit}メソッドは、インスタンスを解放する際に実行される。

\begin{Verbatim}{Chapel}
class Sub {
	const x: real = 0;
	const y: real = 0;
	proc deinit() {
		writeln("deleted");
	}
}
const sub = new Sub(x = 1, y = 2);
writeln("x, y: ", (sub.x, sub.y));
\end{Verbatim}

\section{所有権}

Chapelのクラスには、\textbf{所有権}の概念がある。所有権を持つ変数の参照が消滅したインスタンスは、自動的に解放される。
所有権は、変数宣言で指定する。無修飾または\texttt{owned}で修飾した場合は、その変数がインスタンスの所有権を独占する。

\begin{Verbatim}{Chapel}
class Hello {
	proc deinit() {
		writeln("See you");
	}
}
var hello: owned Hello = new owned Hello();
var hallo: borrowed Hello = hello.borrow();
\end{Verbatim}

\texttt{borrowed}で修飾された変数は、所有権を取得せず、他の\texttt{owned}や\texttt{shared}の変数が所有するインスタンスを参照できる。
\texttt{shared}で修飾した場合は、変数の間でインスタンスが共有される。全ての参照が消滅するとインスタンスが解放される。

\begin{Verbatim}{Chapel}
class Hello {
	proc deinit() {
		writeln("See you");
	}
}
var hello = new shared Hello();
var hallo = new unmanaged Hello();
delete hallo;
\end{Verbatim}

\texttt{unmanaged}で修飾した場合は、所有権による管理を受けず、\texttt{delete}文で、明示的にインスタンスを解放する必要がある。
変数に代入すると、所有権を移譲できる。\texttt{owned}の場合は、所有権が左辺値に移り、\texttt{shared}の場合は、単に共有される。

\section{継承}

クラスを定義する際に、\textbf{親クラス}を指定すると、その親クラスのフィールドやメソッドを継承できる。以下に例を示す。

\begin{Verbatim}{Chapel}
class Foo {
	proc foo() return "foo!";
}
class Bar: Foo {
	override proc foo() return super.foo() + "bar!";
}
const foo = new Foo();
const bar = new Bar();
writeln(foo.foo()); // foo!
writeln(bar.foo()); // foo!bar!
\end{Verbatim}

継承したメソッドの内容を再定義する場合は、\texttt{override}宣言を行う。再定義する前のメソッドは、\texttt{super}で参照できる。

\section{構造的部分型}

以下に示す\texttt{voice}関数は、\texttt{quack}メソッドが定義された、任意の型の値を引数に取る。これを\textbf{ダックタイピング}と呼ぶ。

\begin{Verbatim}{Chapel}
class Duck {
	proc quack() return "quack!";
}
class Kamo {
	proc quack() return "quack!";
}
proc voice(x) return x.quack();
writeln(voice(new Duck())); // quack!
writeln(voice(new Kamo())); // quack!
\end{Verbatim}

\chapter{配列}

Chapelの配列には、\textbf{矩形配列}と\textbf{連想配列}の2種類が存在する。また、類似の概念に、\textbf{タプル}と\textbf{レンジ}と\textbf{領域}が存在する。

\section{タプル}

タプルは、複数の要素をカンマ区切りで並べた値である。値型で、要素の型が揃う必要もなく、配列よりも軽量である。
要素の型を揃えた場合は、要素の数と型の乗算により、タプル型を表現できる。また、\texttt{these}イテレータが利用できる。

\begin{Verbatim}{Chapel}
const nums: 9 * int = (60, 45, 53, 163, 90, 53, 165, 75, 60);
const boys: (string, string, string) = ("Tom", "Ken", "Bob");
writeln(boys(0), boys(1), boys(2), for name in boys do name); // TomKenBobTom Ken Bob
\end{Verbatim}

タプルは、複数の変数を同時に宣言する場合や、値を同時に書き込む場合に利用できる。この機能を\textbf{アンパック}と呼ぶ。

\begin{Verbatim}{Chapel}
var (a, b): (string, int) = ("student", 24);
writeln(a, b);
\end{Verbatim}

タプルのアンパック機能は、関数の宣言でも利用できる。タプルで宣言された引数には、タプルの値を渡す必要がある。

\begin{Verbatim}{Chapel}
proc foo(x: int, (y, z): (int, int)): int return x * (y + z);
writeln(foo(2, (3, 4))); // 14
\end{Verbatim}

\section{レンジ}

レンジは、整数の有限区間や半無限区間や無限区間を表す値である。値型で、同様の機能を持つ領域よりも軽量である。

\begin{Verbatim}{Chapel}
const from100To200: range(int, boundedType = BoundedRangeType.bounded) = 100..200;
const from100ToInf: range(int, boundedType = BoundedRangeType.boundedLow) = 100..;
const fromInfTo200: range(int, boundedType = BoundedRangeType.boundedHigh) = ..20;
\end{Verbatim}

\texttt{by}で刻み幅を指定できる。その場合は、\texttt{align}で指定された値を必ず含む。また、\verb|#|でレンジの要素の個数を指定できる。

\begin{Verbatim}{Chapel}
writeln(10..30 by -7);              // 30 23 16
writeln(10..30 by -7 align 13);     // 27 20 13
writeln(10..30 by -7 align 13 # 2); // 27 20
\end{Verbatim}

レンジに含まれる要素の個数は\texttt{.size}で、区間の下限は\texttt{.low}で、区間の上限は\texttt{.high}で、刻み幅は\texttt{.stride}で得られる。

\begin{Verbatim}{Chapel}
writeln((100..200).size);      // 101
writeln((100..200).low);       // 100
writeln((100..200).high);      // 200
writeln((100..200).stride);    //   1
writeln((100..200).alignment); // 100
\end{Verbatim}

レンジでは、\texttt{these}イテレータが利用できる。\texttt{for}文に限らず、並列化された\texttt{forall}文や\texttt{coforall}文でも利用できる。

\begin{Verbatim}{Chapel}
for i in 1..100 do writeln(i);
forall i in 1..100 do writeln(i);
coforall i in 1..100 do writeln(i);
\end{Verbatim}

\section{領域}

領域は、配列の定義域や値の集合を表す。レンジのタプルと等価な\textbf{矩形領域}と、要素を格納した\textbf{連想領域}の2種類がある。
領域の要素の型は\texttt{.idxType}で取得できる。特に、矩形領域は、\texttt{.rank}で階数を、\texttt{.dims}でレンジのタプルを取得できる。

\begin{Verbatim}{Chapel}
const rectangular: domain = {0..10, -1..2};
const associative: domain = {"foo", "bar"};
writeln(rectangular.rank);
writeln(rectangular.dims());
writeln(rectangular.idxType: string); // int(64)
writeln(associative.idxType: string); // string
\end{Verbatim}

矩形領域も連想領域も、\texttt{these}イテレータが利用できる。特に、矩形領域は、多重ループを表す構文としても利用できる。

\begin{Verbatim}{Chapel}
for xyz in {1..10, 1..10, 1..10} do writeln(xyz);
for boy in {"Tom", "Ken", "Bob"} do writeln(boy);
\end{Verbatim}

\section{配列}

配列は、定義域から値域への写像を表す。矩形領域に対応する\textbf{矩形配列}と、連想領域に対応する\textbf{連想配列}の2種類がある。

\begin{Verbatim}{Chapel}
const rectangular = [1, 2, 3, 4, 5, 6, 7, 8];
const associative = [1 => "one", 2 => "two"];
writeln(rectangular, rectangular.domain); // 1 2 3 4 5 6 7 8{0..7}
writeln(associative, associative.domain); // one two{1, 2}
\end{Verbatim}

\texttt{this}メソッドが利用でき、要素の位置を引数に渡せば、その要素を参照できる。領域を渡せば、部分配列も参照できる。

\begin{Verbatim}{Chapel}
var A: [{1..10, 1..10}] real;
var B: [{'foo', 'bar'}] real;
A[1, 2] = 1.2;
A(3, 4) = 3.4;
writeln(A(1..2, 1..3));
\end{Verbatim}

\texttt{these}イテレータも利用できる。左辺値を返すので、\texttt{for}文でループ変数に値を書き込むと、その値が配列に反映される。

\begin{Verbatim}{Chapel}
var boys = ['Tom', 'Ken', 'Bob'];
for boy in boys do boy += '-san';
writeln(boys); // Tom-san Ken-san Bob-san
\end{Verbatim}

祖な矩形領域を定義域に指定すると、粗な配列を宣言できる。これは、粗行列の実装として活用できる。以下に例を示す。

\begin{Verbatim}{Chapel}
var D: sparse subdomain({1..16, 1..64});
var A: [D] real;
D += (8, 10);
D += (3, 64);
A[8, 10] = 114.514;
\end{Verbatim}

配列は値型であり、配列を他の配列に代入すると、値が複製される。ただし、関数に配列を渡す場合は、参照渡しになる。

\begin{Verbatim}{Chapel}
proc update(arr: [] int) {
	arr = [2, 3, 4];
}
var A = [1, 2, 3];
var B = A;
update(A);
writeln(A); // 2 3 4
writeln(B); // 1 2 3
\end{Verbatim}

\chapter{並列処理}

Chapelは、\href{https://github.com/qthreads/qthreads}{qthreads}の\textbf{軽量スレッド}を採用し、細粒度の並列処理が得意で、\textbf{タスク}の分岐と待機を高効率に実行できる。

\section{タスク}

まず、基本の構文を解説する。\texttt{begin}文でタスクを非同期に実行し、\texttt{sync}文でそのタスクを待機する。以下に例を示す。

\begin{Verbatim}{Chapel}
sync {
	begin writeln("1st parallel task");
	begin writeln("2nd parallel task");
	begin writeln("3rd parallel task");
}
writeln("task 1, 2, 3 are finished");
\end{Verbatim}

\texttt{begin}文は、入れ子にできる。糖衣構文として、\texttt{cobegin}文でも同じ処理を記述できる。通常は\texttt{cobegin}文で事足りる。

\begin{Verbatim}{Chapel}
cobegin {
	writeln("1st parallel task");
	writeln("2nd parallel task");
	writeln("3rd parallel task");
}
writeln("task 1, 2, 3 are finished");
\end{Verbatim}

外側で宣言された変数に、\texttt{begin}文や\texttt{cobegin}文で値を書き戻す場合は、\texttt{with}節の宣言が必要である。以下に例を示す。

\begin{Verbatim}{Chapel}
var a: string;
var b: string;
sync {
	begin with(ref a) a = "1st parallel task";
	begin with(ref b) b = "2nd parallel task";
}
writeln(a);
writeln(b);
\end{Verbatim}

\texttt{sync}文や\texttt{cobegin}文による待機は、僅かな負荷を生じるので、効率を求める場合は、敢えて\texttt{begin}文を使う場合もある。

\begin{Verbatim}{Chapel}
inline proc string.shambles: void {
	proc traverse(a: int, b: int) {
		if b > a {
			const mid = (a + b) / 2;
			begin traverse(a, 0 + mid);
			begin traverse(1 + mid, b);
		} else writeln(this(a));
	}
	sync traverse(0, this.size - 1);
}
\end{Verbatim}

\texttt{serial}文は、条件式が\texttt{true}の場合に、並列処理を逐次処理に切り替える。並列処理の最適化やデバッグに利用できる。

\begin{Verbatim}{Chapel}
serial true {
	begin writeln("1st serial task");
	begin writeln("2nd serial task");
}
\end{Verbatim}

\section{反復処理}

\texttt{forall}文と\texttt{coforall}文は、並列化された\texttt{for}文である。OpenMPの\texttt{omp parallel for}に相当する。以下に例を示す。

\begin{Verbatim}{Chapel}
forall i in 1..100 do writeln(i);
coforall i in 1..100 do writeln(i);
\end{Verbatim}

\texttt{coforall}文は、以下の糖衣構文である。反復回数と同じ個数のタスクを生成する。\textbf{タスク並列}を意識した機能と言える。

\begin{Verbatim}{Chapel}
sync for i in 1..100 do begin writeln(i);
\end{Verbatim}

\texttt{forall}文は、タスクを生成する\textit{leader}と、末端の逐次処理を担当する\textit{follower}の、2個のイテレータで並列処理を行う。

\begin{Verbatim}{Chapel}
iter foo(rng): int {
	for i in rng do yield i;
}
\end{Verbatim}

\texttt{foo}イテレータを多重に定義して、以下の派生型を実装する。これが\textit{leader}で、タスクとデータを再帰的に分岐させる。

\begin{Verbatim}{Chapel}
iter foo(param tag, rng): range where tag == iterKind.leader {
	if rng.size > 16 {
		const mid = (rng.high + rng.low) / 2;
		cobegin {
			for i in foo(tag, rng(..mid+0)) do yield i;
			for i in foo(tag, rng(mid+1..)) do yield i;
		}
	} else yield rng;
}
\end{Verbatim}

また、以下の派生型が\textit{follower}で、並列処理の末端で、細粒度の逐次処理を担当する。データは\texttt{followThis}に渡される。

\begin{Verbatim}{Chapel}
iter foo(param tag, rng, followThis): int where tag == iterKind.follower {
	for i in followThis do yield i;
}
\end{Verbatim}

処理の内容に応じて、最適なタスクとデータの分配方法を選べる点で、\texttt{forall}文は\textbf{データ並列}を意識した機能と言える。

\begin{Verbatim}{Chapel}
forall i in foo(1..100) do writeln(i);
\end{Verbatim}

\section{ロケール\label{sect:locale}}

Chapelでは、分散メモリ環境の構成単位を\textbf{ロケール}と呼ぶ。典型的には、1個の共有メモリ環境がロケールに相当する。
利用可能なロケールは\texttt{Locales}で得られる。また、\href{https://gasnet.lbl.gov}{GASNet}を使う場合は、環境変数\verb|CHPL_COMM|を設定する必要がある。

\begin{Verbatim}{Chapel}
coforall l in Locales do on l {
	writeln(here == l);
	writeln(here.name);
	writeln(here.numPUs());
}
\end{Verbatim}

特定のロケールを指定して、タスクを実行するには、\texttt{on}文を使う。また、\texttt{here}を通じて、現在のロケールを参照できる。
他のロケールに存在するデータを参照すると、\textbf{レイテンシ}が発生する。\texttt{on}文は、参照の局所性を高める目的でも使える。

\begin{Verbatim}{Chapel}
import BigInteger;
var x: BigInteger.bigint;
on Locales(0) do x = new BigInteger.bigint(12);
on x.locale do writeln(x, " is on ", x.locale);
\end{Verbatim}

\section{分散配列}

Chapelの配列のメモリ領域は、\sectref{locale}のロケールに分散して配置できる。分配の方法は、\texttt{dmapped}演算子で指定する。
例えば、\texttt{Block}を選ぶと、配列は矩形の塊で分配される。また、\texttt{localSubdomain}で、そのロケールの領域を取得できる。

\begin{Verbatim}{Chapel}
use BlockDist;
var A: [{1..10,1..10} dmapped Block(boundingBox={1..10,1..10})] real;
for l in Locales do on l do for (i, j) in A.localSubdomain() do writeln(A(i, j).locale);
\end{Verbatim}

\texttt{Cyclic}を選ぶと、周期的に分配される。\texttt{dmapped}で確保した配列は、必要に応じて\texttt{on}文で参照の局所性を確保できる。

\begin{Verbatim}{Chapel}
use CyclicDist;
var B: [{1..10,1..10} dmapped Cyclic(startIdx=(1,1))] real;
for l in Locales do on l do for (i, j) in B.localSubdomain() do writeln(B(i, j).locale);
\end{Verbatim}

\section{アトミック変数}

並列処理で、複数のタスクが共通の変数を読み書きする場合は、\textbf{アトミック演算}で、タスク間の競合を防ぐ必要がある。
競合の例を以下に示す。変数\texttt{sum}の値を取得し、新たな値を書き戻す間に、\texttt{sum}の値が変化すれば、誤った結果になる。

\begin{Verbatim}{Chapel}
config const N = 80;
var sum: int;
do {
	coforall n in 1..N with(ref sum) do sum -= n * n;
	coforall n in 1..N with(ref sum) do sum += n * n;
} while sum == 0;
writeln(sum);
\end{Verbatim}

以下に、適切な実装を示す。\texttt{add}や\texttt{sub}の代わりに、\texttt{fetchAdd}や\texttt{fetchSub}を使えば、演算する直前の値も取得できる。

\begin{Verbatim}{Chapel}
config const N = 80;
var sum: atomic int;
do {
	coforall n in 1..N do sum.sub(n * n);
	coforall n in 1..N do sum.add(n * n);
} while sum.read() == 0; // infinite loop
writeln(sum);
\end{Verbatim}

\section{ロック付き変数}

\texttt{sync}型の変数を参照するには、\texttt{readFE}を使う。ただし、変数の状態が\textit{empty}の場合は、\textit{full}に遷移するまで待機となる。
\texttt{writeEF}で値を書き込むと、状態が\textit{full}に遷移する。これは\textbf{排他制御}の機能であり、タスク間の競合を防ぐ効果がある。

\begin{Verbatim}{Chapel}
config const N = 80;
var sum: sync int = 0;
do {
	coforall n in 1..N do {
		const v = sum.readFE();
		sum.writeEF(v + n * n);
	}
	coforall n in 1..N do {
		const v = sum.readFE();
		sum.writeEF(v - n * n);
	}
} while sum.readFF() == 0; // infinite loop
writeln(sum.readFF());
\end{Verbatim}

\end{document}
